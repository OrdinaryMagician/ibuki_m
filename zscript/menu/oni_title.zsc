// Things for the titlemap

Struct TitleSmoke
{
	int age, maxage;
	Vector2 pos, vel;
	double ang, angvel;
	double scale;
}

Class OniTitleStuff : EventHandler
{
	ui TextureID bgtex;
	ui TextureID ibukitex[4];
	ui TitleSmoke smk[128];
	ui TextureID smk_tex[8];
	ui TextureID logotex[4];
	ui TextureID gradtex;
	ui int quakestr;
	ui int rss;
	int titletimer;
	bool bFadeFromBlack;
	ui Font mSmallFont;

	private ui int GetUIRandom()
	{
		return (rss = (rss<<1)*35447+(rss/87));
	}

	private ui double RandomShiver()
	{
		int sd = GetUIRandom();
		return ((abs(sd)%11)-5)*.1;
	}

	private ui void TickSmokeLayers()
	{
		for ( int i=0; i<128; i++ )
		{
			int presim = 0;
			if ( smk[i].age >= smk[i].maxage )
			{
				if ( smk[i].maxage == 0 ) presim = Random[UIStuff](0,60);
				else presim = -1;
				smk[i].age = Random[UIStuff](-20,0);
				smk[i].maxage = Random[UIStuff](60,120);
				smk[i].pos = (FRandom[UIStuff](-512.,512.),FRandom[UIStuff](0.,256.));
				smk[i].vel = (FRandom[UIStuff](-1.,3.),FRandom[UIStuff](-2.,-6.));
				smk[i].ang = FRandom[UIStuff](0.,360.);
				smk[i].angvel = FRandom[UIStuff](1.,4.)*RandomPick[UIStuff](-1,1);
				smk[i].scale = FRandom[UIStuff](2.,4.);
			}
			for ( int j=0; j<=presim; j++ )
			{
				smk[i].age++;
				if ( smk[i].age <= 0 ) continue;
				smk[i].pos += smk[i].vel;
				smk[i].ang += smk[i].angvel;
			}
		}
	}

	override void WorldTick()
	{
		titletimer++;
	}

	override void WorldLoaded( WorldEvent e )
	{
		titletimer = 0;
		let shnd = SWWMStaticHandler(StaticEventHandler.Find("SWWMStaticHandler"));
		if ( !shnd ) return;
		if ( shnd.titlefirst )
		{
			titletimer = GameTicRate*44+1;	// skip to logo (also skip the fire sound)
			bFadeFromBlack = true;	// fade from black instead of white
			S_ChangeMusic("music/ibuki_pretitlemus_skip.ogg",looping:false);
		}
		shnd.titlefirst = true;
	}

	override void UiTick()
	{
		quakestr = max(quakestr-1,0);
		if ( titletimer == 1 ) S_ChangeMusic("music/ibuki_pretitlemus.ogg",looping:false);
		else if ( titletimer == (43*GameTicRate) ) S_StartSound("aa12/titlecock",CHAN_WEAPON,CHANF_OVERLAP|CHANF_UI,1.,0.);
		else if ( titletimer == (44*GameTicRate) ) S_StartSound("aa12/titlefire",CHAN_WEAPON,CHANF_OVERLAP|CHANF_UI,1.,0.);
		else if ( titletimer == (46*GameTicRate) )
		{
			quakestr += 30;
			S_StartSound("title/thump",CHAN_BODY,CHANF_OVERLAP|CHANF_UI,1.,0.,1.);
		}
		else if ( titletimer == (47*GameTicRate) )
		{
			quakestr += 30;
			S_StartSound("title/thump",CHAN_BODY,CHANF_OVERLAP|CHANF_UI,1.,0.,.8);
		}
		else if ( titletimer == (50*GameTicRate) ) S_ChangeMusic("music/ibuki_titlemus.ogg");
		TickSmokeLayers();
		if ( gamestate != GS_LEVEL ) return;
		// you're not supposed to be here
		Menu.SetMenu('EndGameMenu');
		let m = MessageBoxMenu(Menu.GetCurrentMenu());
		if ( m ) m.HandleResult(true);
		// special thanks to marrub for this absolute crime
	}

	// heh
	private clearscope double smerp( double a ) const
	{
		return (sin((a-.5)*180.)+1.)/2.;
	}

	private ui void RenderSmokeLayer( RenderEvent e, double GameSecs, bool back, double alpha = 1., double disp = 0., bool zoomout = false )
	{
		double zoomscl = Screen.GetHeight()/(zoomout?600:400.);
		int imin = back?64:0;
		int imax = back?128:64;
		int base = zoomout?(back?450:750):(back?500:300);
		for ( int i=imin; i<imax; i++ )
		{
			if ( !smk[i].maxage ) continue;
			if ( !smk_tex[i%8] ) smk_tex[i%8] = TexMan.CheckForTexture("graphics/title_smoke_"..(i%8)..".png",TexMan.Type_Any);
			Vector2 pos = (smk[i].pos+smk[i].vel*e.FracTic+(0,base+disp))*zoomscl+(Screen.GetWidth()/2,0);
			double scl = smk[i].scale*zoomscl;
			double ang = smk[i].ang+smk[i].angvel*e.FracTic;
			double alf = cos((smk[i].age/double(smk[i].maxage))*90)*alpha;
			alf *= clamp(smk[i].age/10.,0,1);
			Screen.DrawTexture(smk_tex[i%8],false,pos.x,pos.y,DTA_CenterOffset,true,DTA_Rotate,ang,DTA_ScaleX,scl,DTA_ScaleY,scl,DTA_Alpha,alf,DTA_LegacyRenderStyle,STYLE_AddShaded,DTA_FillColor,Color(48,16,4));
		}
	}

	override void RenderUnderlay( RenderEvent e )
	{
		if ( !ibukitex[0] ) ibukitex[0] = TexMan.CheckForTexture("graphics/title_ibuki_dark.png",TexMan.Type_Any);
		if ( !ibukitex[1] ) ibukitex[1] = TexMan.CheckForTexture("graphics/title_ibuki_eye.png",TexMan.Type_Any);
		if ( !ibukitex[2] ) ibukitex[2] = TexMan.CheckForTexture("graphics/title_ibuki.png",TexMan.Type_Any);
		if ( !ibukitex[3] ) ibukitex[3] = TexMan.CheckForTexture("graphics/title_ibuki_smoke.png",TexMan.Type_Any);
		if ( !logotex[0] ) logotex[0] = TexMan.CheckForTexture("graphics/OniLogoL.png",TexMan.Type_Any);
		if ( !logotex[1] ) logotex[1] = TexMan.CheckForTexture("graphics/OniLogo.png",TexMan.Type_Any);
		if ( !logotex[2] ) logotex[2] = TexMan.CheckForTexture("graphics/OniLogoL_Glow.png",TexMan.Type_Any);
		if ( !logotex[3] ) logotex[3] = TexMan.CheckForTexture("graphics/OniLogoR_Glow.png",TexMan.Type_Any);
		if ( !gradtex ) gradtex = TexMan.CheckForTexture("graphics/title_grad.png",TexMan.Type_Any);
		if ( !mSmallFont ) mSmallFont = Font.GetFont('TewiFont');
		double GameSecs = (titletimer+e.FracTic)/GameTicRate;
		if ( swwm_fuzz )
		{
			if ( !bgtex ) bgtex = TexMan.CheckForTexture("graphics/tempbg.png",TexMan.Type_Any);
			Vector2 tsize = TexMan.GetScaledSize(bgtex);
			double zoom = max(ceil(Screen.GetWidth()/tsize.x),ceil(Screen.GetHeight()/tsize.y));
			Vector2 vsize = (Screen.GetWidth(),Screen.GetHeight())/zoom;
			Screen.DrawTexture(bgtex,false,(vsize.x-tsize.x)/2,(vsize.y-tsize.y)/2,DTA_VirtualWidthF,vsize.x,DTA_VirtualHeightF,vsize.y,DTA_KeepRatio,true,DTA_ColorOverlay,Color(128,0,0,0));
			Screen.Dim(Color(16,0,0),1.-clamp((GameSecs-44),.1,1.),0,0,Screen.GetWidth(),Screen.GetHeight());
		}
		else Screen.Dim(Color(16,0,0),1.,0,0,Screen.GetWidth(),Screen.GetHeight());
		double alf;
		String str;
		double zoomscl = Screen.GetHeight()/400.;
		double texscl = Screen.GetHeight()/2700.;
		double logoscl = Screen.GetHeight()/1024.;
		// intro texts
		if ( titletimer < (42*GameTicRate) )
		{
			alf = clamp(GameSecs/40.,.2,1.);
			double disp = smerp(clamp((GameSecs-16)/25.,0,1));
			RenderSmokeLayer(e,GameSecs,true,clamp(alf*2.,.2,1.),200.*disp);
			Screen.DrawTexture(ibukitex[0],false,Screen.GetWidth()/2+zoomscl*50,Screen.GetHeight()+zoomscl*1700.*disp,DTA_ScaleX,zoomscl,DTA_ScaleY,zoomscl,DTA_ColorOverlay,Color(255-int(alf*255),0,0,0));
			RenderSmokeLayer(e,GameSecs,false,clamp(alf*2.,.2,1.)*.5,400.*disp);
			alf = clamp(GameSecs-1,0.,1.);
			alf *= 1.-clamp(GameSecs-6,0.,1.);
			String str = StringTable.Localize("$ONI_TITLEPRESENTSA");
			Screen.DrawText(mSmallFont,Font.CR_DARKRED,(Screen.GetWidth()-mSmallFont.StringWidth(str)*CleanXFac)/2,(Screen.GetHeight()/2)-(mSmallFont.GetHeight()*CleanYFac),str,DTA_CleanNoMove,true,DTA_Alpha,alf);
			str = StringTable.Localize("$ONI_TITLEPRESENTSB");
			Screen.DrawText(mSmallFont,Font.CR_BLACK,(Screen.GetWidth()-mSmallFont.StringWidth(str)*CleanXFac)/2,(Screen.GetHeight()/2),str,DTA_CleanNoMove,true,DTA_Alpha,alf);
			alf = clamp(GameSecs-8,0.,1.);
			alf *= 1.-clamp(GameSecs-13,0.,1.);
			str = StringTable.Localize("$ONI_TITLEMODBYA");
			Screen.DrawText(mSmallFont,Font.CR_BLACK,(Screen.GetWidth()-mSmallFont.StringWidth(str)*CleanXFac)/2,(Screen.GetHeight()/2)-(mSmallFont.GetHeight()*CleanYFac),str,DTA_CleanNoMove,true,DTA_Alpha,alf);
			str = StringTable.Localize("$ONI_TITLEMODBYB");
			Screen.DrawText(mSmallFont,Font.CR_DARKRED,(Screen.GetWidth()-mSmallFont.StringWidth(str)*CleanXFac)/2,(Screen.GetHeight()/2),str,DTA_CleanNoMove,true,DTA_Alpha,alf);
			alf = clamp(GameSecs-20,0.,1.);
			alf *= 1.-clamp(GameSecs-35,0.,1.);
			str = StringTable.Localize("$ONI_TITLEINTROA");
			Screen.DrawText(mSmallFont,Font.CR_BLACK,(Screen.GetWidth()-mSmallFont.StringWidth(str)*CleanXFac)/2,(Screen.GetHeight()/2)-(mSmallFont.GetHeight()*CleanYFac),str,DTA_CleanNoMove,true,DTA_Alpha,alf);
			str = StringTable.Localize("$ONI_TITLEINTROB");
			Screen.DrawText(mSmallFont,Font.CR_BLACK,(Screen.GetWidth()-mSmallFont.StringWidth(str)*CleanXFac)/2,(Screen.GetHeight()/2),str,DTA_CleanNoMove,true,DTA_Alpha,alf);
		}
		// 42 seconds in: eye glow
		else if ( titletimer < (44*GameTicRate) )
		{
			RenderSmokeLayer(e,GameSecs,true,1.,200.);
			Screen.DrawTexture(ibukitex[0],false,Screen.GetWidth()/2+zoomscl*50,Screen.GetHeight()+zoomscl*1700,DTA_ScaleX,zoomscl,DTA_ScaleY,zoomscl);
			RenderSmokeLayer(e,GameSecs,false,.5,400.);
			alf = clamp((GameSecs-43)*4.,0.,1.);
			Screen.DrawTexture(ibukitex[1],false,Screen.GetWidth()/2+zoomscl*50,Screen.GetHeight()+zoomscl*1700,DTA_ScaleX,zoomscl,DTA_ScaleY,zoomscl,DTA_Alpha,alf,DTA_LegacyRenderStyle,STYLE_Add);
			alf *= 1.-clamp((GameSecs-43),0.,1.);
			Screen.Dim("Red",(alf**2)*.3,0,0,Screen.GetWidth(),Screen.GetHeight());
		}
		// 44 seconds in: fade from white to full title scene
		else
		{
			if ( titletimer > 47*GameTicRate )
				Screen.DrawTexture(logotex[1],false,Screen.GetWidth()/2,-80*logoscl,DTA_ScaleX,logoscl,DTA_ScaleY,logoscl,DTA_LeftOffset,1024);
			else if ( titletimer > 46*GameTicRate )
				Screen.DrawTexture(logotex[0],false,Screen.GetWidth()/2,-80*logoscl,DTA_ScaleX,logoscl,DTA_ScaleY,logoscl,DTA_LeftOffset,1024);
			if ( (titletimer > 46*GameTicRate) && (titletimer < 48*GameTicRate) )
				Screen.DrawTexture(logotex[2],false,Screen.GetWidth()/2,-80*logoscl,DTA_ScaleX,logoscl,DTA_ScaleY,logoscl,DTA_LeftOffset,1024,DTA_LegacyRenderStyle,STYLE_Add,DTA_Alpha,1.-clamp(GameSecs-46,0.,1.));
			if ( (titletimer > 47*GameTicRate) && (titletimer < 49*GameTicRate) )
				Screen.DrawTexture(logotex[3],false,Screen.GetWidth()/2,-80*logoscl,DTA_ScaleX,logoscl,DTA_ScaleY,logoscl,DTA_LeftOffset,1024,DTA_LegacyRenderStyle,STYLE_Add,DTA_Alpha,1.-clamp(GameSecs-47,0.,1.));
			Screen.DrawTexture(gradtex,false,0,Screen.GetHeight(),DTA_DestWidth,Screen.GetWidth(),DTA_DestHeight,256*CleanYFac_1,DTA_LegacyRenderStyle,STYLE_AddShaded,DTA_FillColor,Color(128,0,0),DTA_TopOffset,256);
			RenderSmokeLayer(e,GameSecs,true,.5,0,true);
			alf = clamp((GameSecs-44.5)*1.5,0.,1.);
			rss = int(MSTimeF()/GameTicRate);
			Vector2 ofs = (RandomShiver(),RandomShiver())*quakestr*texscl;
			if ( alf < 1. )
			{
				Screen.DrawTexture(ibukitex[0],false,Screen.GetWidth()/2+60*texscl+ofs.x,Screen.GetHeight()+80*texscl+ofs.y,DTA_ScaleX,texscl,DTA_ScaleY,texscl,DTA_Alpha,1.-alf);
				Screen.DrawTexture(ibukitex[2],false,Screen.GetWidth()/2+60*texscl+ofs.x,Screen.GetHeight()+80*texscl+ofs.y,DTA_ScaleX,texscl,DTA_ScaleY,texscl,DTA_Alpha,alf);
			}
			else Screen.DrawTexture(ibukitex[2],false,Screen.GetWidth()/2+60*texscl+ofs.x,Screen.GetHeight()+80*texscl+ofs.y,DTA_ScaleX,texscl,DTA_ScaleY,texscl);
			alf = clamp((GameSecs-44.)*.5,0.,1.);
			Screen.DrawTexture(ibukitex[3],false,Screen.GetWidth()/2+60*texscl+ofs.x,Screen.GetHeight()+80*texscl+ofs.y,DTA_ScaleX,texscl,DTA_ScaleY,texscl,DTA_LegacyRenderStyle,STYLE_Add,DTA_Alpha,alf*.5);
			Screen.DrawTexture(ibukitex[1],false,Screen.GetWidth()/2+60*texscl+ofs.x,Screen.GetHeight()+80*texscl+ofs.y,DTA_ScaleX,texscl,DTA_ScaleY,texscl,DTA_LegacyRenderStyle,STYLE_Add);
			RenderSmokeLayer(e,GameSecs,false,1.,0,true);
			Screen.DrawTexture(gradtex,false,0,Screen.GetHeight(),DTA_DestWidth,Screen.GetWidth(),DTA_DestHeight,64*CleanYFac_1,DTA_LegacyRenderStyle,STYLE_Shaded,DTA_FillColor,Color(128,0,0),DTA_TopOffset,256);
			if ( titletimer < (45*GameTicRate) )
			{
				alf = 1.-clamp((GameSecs-44),0.,1.);
				Screen.Dim(bFadeFromBlack?"Black":"White",alf,0,0,Screen.GetWidth(),Screen.GetHeight());
			}
			if ( quakestr > 0. )
			{
				alf = (quakestr-e.fractic)/60.;
				Screen.Dim(Color(255,64,0),alf,0,0,Screen.GetWidth(),Screen.GetHeight());
			}
		}
		// global fade in from back
		if ( titletimer < (1*GameTicRate) )
			Screen.Dim("Black",1.-clamp(GameSecs,0.,1.),0,0,Screen.GetWidth(),Screen.GetHeight());
	}
}
