// intermission stat screen
Class OniStatScreen : SWWMStatScreen
{
	TextureID bgtex;
	TextureID ibukitex[3];
	TitleSmoke smk[128];
	TextureID smk_tex[8];

	override void Start( wbstartstruct wbstartstruct )
	{
		Super.Start(wbstartstruct);
		lnamecolor = Font.CR_WHITE;
		lauthcolor = Font.CR_DARKGRAY;
		lsubcolor = Font.CR_DARKRED;
		statbasecolor = Font.CR_DARKRED;
		statcolor0 = Font.CR_DARKGRAY;
		statcolor1 = Font.CR_WHITE;
		statcolor2 = Font.CR_RED;
		tipcolor0 = Font.CR_RED;
		tipcolor1 = Font.CR_WHITE;
	}

	override void StartMusic()
	{
		S_ChangeMusic("music/ibuki_intermus.ogg");
	}

	override String StatCnt( int a, int b )
	{
		if ( b <= 0 ) return "N/A";
		return String.Format("%s \cm/\c- %s \cm(\c-%3d%%\cm)",SWWMUtility.ThousandsNum(max(a,0)),SWWMUtility.ThousandsNum(b),GetPct(a,b));
	}

	override String TimeStr( int secs )
	{
		secs = max(secs,0);
		int h = secs/3600;
		int m = (secs/60)%60;
		int s = secs%60;
		if ( h ) return String.Format("%d\cm:\c-%02d\cm:\c-%02d",h,m,s);
		if ( m ) return String.Format("%d\cm:\c-%02d",m,s);
		return String.Format("%d",s);
	}

	override void Ticker( void )
	{
		Super.Ticker();
		TickSmokeLayers();
	}

	private void TickSmokeLayers()
	{
		for ( int i=0; i<128; i++ )
		{
			int presim = 0;
			if ( smk[i].age >= smk[i].maxage )
			{
				if ( smk[i].maxage == 0 ) presim = Random[UIStuff](0,60);
				else presim = -1;
				smk[i].age = Random[UIStuff](-20,0);
				smk[i].maxage = Random[UIStuff](60,120);
				smk[i].pos = (FRandom[UIStuff](-512.,512.),FRandom[UIStuff](0.,256.));
				smk[i].vel = (FRandom[UIStuff](-1.,3.),FRandom[UIStuff](-2.,-6.));
				smk[i].ang = FRandom[UIStuff](0.,360.);
				smk[i].angvel = FRandom[UIStuff](1.,4.)*RandomPick[UIStuff](-1,1);
				smk[i].scale = FRandom[UIStuff](2.,4.);
			}
			for ( int j=0; j<=presim; j++ )
			{
				smk[i].age++;
				if ( smk[i].age <= 0 ) continue;
				smk[i].pos += smk[i].vel;
				smk[i].ang += smk[i].angvel;
			}
		}
	}
	private void RenderSmokeLayer( double GameSecs, bool back, double alpha = 1., double disp = 0., bool zoomout = false )
	{
		double zoomscl = Screen.GetHeight()/(zoomout?600:400.);
		int imin = back?64:0;
		int imax = back?128:64;
		int base = zoomout?(back?450:750):(back?500:300);
		for ( int i=imin; i<imax; i++ )
		{
			if ( !smk_tex[i%8] ) smk_tex[i%8] = TexMan.CheckForTexture("graphics/title_smoke_"..(i%8)..".png",TexMan.Type_Any);
			Vector2 pos = (smk[i].pos+smk[i].vel*FracTic+(0,base+disp))*zoomscl+(Screen.GetWidth()/2,0);
			double scl = smk[i].scale*zoomscl;
			double ang = smk[i].ang+smk[i].angvel*FracTic;
			double alf = cos((smk[i].age/double(smk[i].maxage))*90)*alpha;
			alf *= clamp(smk[i].age/10.,0,1);
			Screen.DrawTexture(smk_tex[i%8],false,pos.x,pos.y,DTA_CenterOffset,true,DTA_Rotate,ang,DTA_ScaleX,scl,DTA_ScaleY,scl,DTA_Alpha,alf,DTA_LegacyRenderStyle,STYLE_AddShaded,DTA_FillColor,Color(48,16,4));
		}
	}
	// heh
	private clearscope double smerp( double a ) const
	{
		return (sin((a-.5)*180.)+1.)/2.;
	}
	override void drawSWWMBg()
	{
		double GameSecs = (bcnt+FracTic)/GameTicRate;
		if ( oni_fuzz )
		{
			if ( !bgtex ) bgtex = TexMan.CheckForTexture("graphics/tempbg.png",TexMan.Type_Any);
			Vector2 tsize = TexMan.GetScaledSize(bgtex);
			double zoom = max(ceil(Screen.GetWidth()/tsize.x),ceil(Screen.GetHeight()/tsize.y));
			Vector2 vsize = (Screen.GetWidth(),Screen.GetHeight())/zoom;
			Screen.DrawTexture(bgtex,false,(vsize.x-tsize.x)/2,(vsize.y-tsize.y)/2,DTA_VirtualWidthF,vsize.x,DTA_VirtualHeightF,vsize.y,DTA_KeepRatio,true,DTA_ColorOverlay,Color(128,0,0,0));
			Screen.Dim(Color(16,0,0),1.-clamp(GameSecs,.1,1.),0,0,Screen.GetWidth(),Screen.GetHeight());
		}
		else Screen.Dim(Color(16,0,0),1.,0,0,Screen.GetWidth(),Screen.GetHeight());
		double texscl = Screen.GetHeight()/2200.;
		if ( !gradtex ) gradtex = TexMan.CheckForTexture("graphics/title_grad.png",TexMan.Type_Any);
		Screen.DrawTexture(gradtex,false,0,Screen.GetHeight(),DTA_DestWidth,Screen.GetWidth(),DTA_DestHeight,256*CleanYFac_1,DTA_LegacyRenderStyle,STYLE_AddShaded,DTA_FillColor,Color(128,0,0),DTA_TopOffset,256);
		RenderSmokeLayer(GameSecs,true,.5,0,true);
		if ( !ibukitex[0] ) ibukitex[0] = TexMan.CheckForTexture("graphics/title_ibuki.png",TexMan.Type_Any);
		if ( !ibukitex[1] ) ibukitex[1] = TexMan.CheckForTexture("graphics/title_ibuki_smoke.png",TexMan.Type_Any);
		if ( !ibukitex[2] ) ibukitex[2] = TexMan.CheckForTexture("graphics/title_ibuki_eye.png",TexMan.Type_Any);
		double ofs;
		if ( sp_state >= 2 ) ofs = 120;
		else ofs = 120*smerp(clamp(GameSecs,0.,1.));
		Screen.DrawTexture(ibukitex[0],false,Screen.GetWidth()/2+ofs*CleanXFac_1,Screen.GetHeight()+450*texscl,DTA_ScaleX,texscl,DTA_ScaleY,texscl);
		Screen.DrawTexture(ibukitex[1],false,Screen.GetWidth()/2+ofs*CleanXFac_1,Screen.GetHeight()+450*texscl,DTA_ScaleX,texscl,DTA_ScaleY,texscl,DTA_LegacyRenderStyle,STYLE_Add,DTA_Alpha,.5);
		Screen.DrawTexture(ibukitex[2],false,Screen.GetWidth()/2+ofs*CleanXFac_1,Screen.GetHeight()+450*texscl,DTA_ScaleX,texscl,DTA_ScaleY,texscl,DTA_LegacyRenderStyle,STYLE_Add);
		double alf = clamp(glarestr/20.,0.,1.)**2;
		if ( alf > 0. ) Screen.DrawTexture(ibukitex[2],false,Screen.GetWidth()/2+ofs*CleanXFac_1,Screen.GetHeight()+450*texscl,DTA_ScaleX,texscl,DTA_ScaleY,texscl,DTA_LegacyRenderStyle,STYLE_Add,DTA_Alpha,alf);
		RenderSmokeLayer(GameSecs,false,1.,0,true);
		Screen.DrawTexture(gradtex,false,0,Screen.GetHeight(),DTA_DestWidth,Screen.GetWidth(),DTA_DestHeight,64*CleanYFac_1,DTA_LegacyRenderStyle,STYLE_Shaded,DTA_FillColor,Color(128,0,0),DTA_TopOffset,256);
	}
}
